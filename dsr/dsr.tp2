BACKUP ~dsr/backup~        
//AUTHOR ~TIN, Badgert (WeiDU conversion)~ 
SUPPORT ~~

VERSION ~v8.0.0~

README ~dsr/readme/dsr-readme-%%.txt~ ~dsr/readme/dsr-readme-english.txt~


ALWAYS

	/* Converts tra files to UTF-8 */
	ACTION_DEFINE_ARRAY xo_reload BEGIN setup.tra END
	ACTION_DEFINE_ARRAY xo_noconvert BEGIN END
		LAF HANDLE_CHARSETS
			INT_VAR
			infer_charset = 1
			STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/tra~
			noconvert_array = xo_noconvert
			reload_array = xo_reload
		END
	END

	/* Loads in any shared functions library (general macros and functions) */
	INCLUDE ~heartwood/lib/gw_functions.tpa~	// Loads macros and functions library dealing with tooltip appending and specific EE items descriptions.

END

AUTO_TRA ~dsr/tra/%s~

LANGUAGE ~English~
		 ~english~
         ~dsr/tra/english/setup.tra~

LANGUAGE ~Russian (aerie.ru)~
		 ~russian~
         ~dsr/tra/russian/setup.tra~

LANGUAGE ~Francais (par Deratiseur)~
		 ~french~
         ~dsr/tra/french/setup.tra~ 

LANGUAGE ~Italian (Ilot)~
		 ~italian~
         ~dsr/tra/italian/setup.tra~

LANGUAGE ~Deutsch (Jarl)~
		 ~german~
         ~dsr/tra/german/setup.tra~


/* ================================================================ *
 *    Demon Summoning Ritual - (Unholy Gate Opening Ritual Book)    *
 * ================================================================ */

BEGIN @0
DESIGNATED 0
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1

// Install items
COPY ~dsr/itm/sumdem.itm~	~override~  
	SAY NAME1 @11 
	SAY NAME2 @12 
	SAY UNIDENTIFIED_DESC @13 
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@14" END

// Install creatures
COPY ~dsr/cre/balorsu.cre~	~override~
COPY ~dsr/cre/demcorsu.cre~	~override~
	SAY NAME1 @21
	SAY NAME2 @21

//COPY ~dsr/cre/tolger2.cre~       ~override/tolger2.cre~
COPY_EXISTING ~tolger2.cre~ ~override~
	PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
		WRITE_BYTE 0x72 0
		WRITE_BYTE 0x83 100
		WRITE_BYTE 0x237 2
		ADD_KNOWN_SPELL ~SPWI113~ #0 ~wizard~
		ADD_KNOWN_SPELL ~SPWI212~ #1 ~wizard~
		ADD_KNOWN_SPELL ~SPWI313~ #2 ~wizard~
		ADD_KNOWN_SPELL ~SPWI712~ #6 ~wizard~
		ADD_KNOWN_SPELL ~SPWI815~ #7 ~wizard~
		ADD_KNOWN_SPELL ~SPWI909~ #8 ~wizard~
		ADD_KNOWN_SPELL ~SPWI910~ #8 ~wizard~
		ADD_KNOWN_SPELL ~SPWI911~ #8 ~wizard~
		ADD_MEMORIZED_SPELL ~SPWI113~ #0 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI212~ #1 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI313~ #2 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI712~ #6 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI815~ #7 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI909~ #8 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI910~ #8 ~wizard~ (1)
		ADD_MEMORIZED_SPELL ~SPWI911~ #8 ~wizard~ (1)
		ADD_CRE_ITEM ~SUMDEM~ #1 #0 #0 ~NONE~ ~QUIVER2~
	END
BUT_ONLY 

COPY ~dsr/eff~ ~override~

// Install scripts
//COPY_EXISTING ~tolger.bcs~	~override/tolger.bcs~
EXTEND_BOTTOM ~tolger.bcs~	~dsr/scripts/stolger.baf~ 

COMPILE ~dsr/baf~

// End of Demon Summoning Ritual
